create path (nt:folder) /libs
create path (nt:folder) /apps
create path (nt:folder) /tmp
create service user user1, u-ser_2, u3

register privilege some:otherPrivilege

register nodetypes
<<===
<'sling'='http://sling.apache.org/jcr/sling/1.0'>

[sling:Folder] > nt:folder, nt:unstructured
===>>


set ACL on /libs,/apps
    allow jcr:read for user1,u-ser_2

    deny jcr:write for u-ser_2
    deny jcr:lockManagement for user1
    remove jcr:read,some:otherPrivilege for u3
end

create service user bob_the_service

set ACL on /tmp
    allow some:otherPrivilege for bob_the_service
end

# Nodetypes inside the path apply to just that path element
create path /content/example.com(sling:Folder)

# Nodetypes and mixins applied to just a path element
# Specifying mixins require
# o.a.s.repoinit.parser 1.2.0 and
# o.a.s.jcr.repoinit 1.1.6
create path /content/example.com(sling:Folder mixin mix:referenceable,mix:shareable)

# Mixins applied to just a path element
create path /content/example.com(mixin mix:referenceable)

# A nodetype in front is used as the default for all path elements
create path (nt:unstructured) /var

create user alice with password {SHA-256} dc460da4ad72c482231e28e688e01f2778a88ce31a08826899d54ef7183998b5
create user bob with password {SHA-256} dc460da4ad72c482231e28e688e01f2778a88ce31a08826899d54ef7183998b5
create user carol with password {SHA-256} dc460da4ad72c482231e28e688e01f2778a88ce31a08826899d54ef7183998b5
create user fred with password {SHA-256} dc460da4ad72c482231e28e688e01f2778a88ce31a08826899d54ef7183998b5
create user larry with password {SHA-256} dc460da4ad72c482231e28e688e01f2778a88ce31a08826899d54ef7183998b5

set ACL for alice, bob,fred
    # remove is currently not supported by the jcr.repoinit module
    # remove * on /
    allow jcr:read on /content,/var
    # PrincipalAccessControlList doesn't support 'deny' entries.
    deny jcr:write on /content/example.com
    deny jcr:all on / nodetypes example:Page
end

create service user restrictions_examples
set ACL for restrictions_examples
    # PrincipalAccessControlList doesn't support 'deny' entries.
    deny jcr:modifyProperties on /apps, /content nodetypes sling:Folder, nt:unstructured restriction(rep:itemNames,prop1,prop2)
    allow jcr:addChildNodes on /apps restriction(rep:ntNames,sling:Folder,nt:unstructured)
    allow jcr:modifyProperties on /apps restriction(rep:ntNames,sling:Folder,nt:unstructured) restriction(rep:itemNames,prop1,prop2)
    allow jcr:addChildNodes on /apps,/content restriction(rep:glob,/cat/*)

    # empty rep:glob means "apply to this node but not its children"
    # (requires o.a.s.jcr.repoinit 1.1.8)
    allow jcr:read on / restriction(rep:glob)
end

# Set repository level ACL
# Setting repository level ACL require
# o.a.s.repoinit.parser 1.2.0 and
# o.a.s.jcr.repoinit 1.1.6
set repository ACL for alice,bob
    allow jcr:namespaceManagement,jcr:nodeTypeDefinitionManagement
end

# Set repository level ACL (variant, see SLING-8619)
# since
# o.a.s.repoinit.parser 1.2.8 and
# o.a.s.jcr.repoinit 1.1.14
set ACL for alice,bob
    allow jcr:namespaceManagement on :repository
end

# register namespace requires
# o.a.s.repoinit.parser 1.0.4
# and o.a.s.jcr.repoinit 1.0.2
register namespace ( some ) http://some.com/ns/1.0

# register nodetypes in CND format
# (same bundle requirements as register namespaces)
#
# The optional << markers are used when embedding
# this in a Sling provisioning model, to avoid syntax errors
#
# The CND instructions are passed as is to the JCR
# modules, so the full CND syntax is supported.
#
register nodetypes
<<===
<slingevent='http://sling.apache.org/jcr/event/1.0'>

[slingevent:Event] > nt:unstructured, nt:hierarchyNode
  - slingevent:topic (string)
  - slingevent:properties (binary)
===>>

create user demoUser with password {SHA-256} dc460da4ad72c482231e28e688e01f2778a88ce31a08826899d54ef7183998b5

# disable service user
create service user deprecated_service_user
disable service user deprecated_service_user : "Disabled user to make an example"

create service user the-last-one

disable service user svc1 : "This  is the message"

# Groups are supported since version 1.2.4, SLING-8219
create group since124_A
create group since124_B with path /home/groups/path_B
delete group since124_C

# Manage principals in groups, requires
# o.a.s.repoinit.parser 1.5.2
# and o.a.s.jcr.repoinit 1.1.22
create group grpA
create group grpB
add user1,user2 to group grpA
remove user3,user5 from group grpB

# ACLs on user homes, requires
# o.a.s.repoinit.parser 1.4.2
# o.a.s.jcr.repoinit 1.1.18
set ACL on home(alice)
  allow jcr:read for alice, bob, carol
end

create path /another/path
set ACL for bob
  allow jcr:read on home(alice), /another/path, home(larry)
end

# Set node properties, requires
# o.a.s.repoinit.parser 1.6.2
# o.a.s.jcr.repoinit 1.1.24
#
# 'set' overwrites any existing value while
# 'default' only sets the property if not set yet
#
# The paths must exist first, see "create path"
create path (nt:unstructured) /pathA
create path (nt:unstructured) /path/B
set properties on /pathA, /path/B
  set sling:ResourceType{String} to /x/y/z
  default someInteger{Long} to 42
  set someFlag{Boolean} to true
  default someDate{Date} to "2020-03-19T11:39:33.437+05:30"
  set quotedMix to "quoted", non-quoted, "the last \" one"
end